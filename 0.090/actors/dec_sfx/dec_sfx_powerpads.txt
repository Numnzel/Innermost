////////////////////////////////////////
// POWERUP POD STRING FOUNTAINS ////////
// for STRONGHOLD		////////
// by Tormentor667		////////
////////////////////////////////////////

Actor BaseLine {
	
	Radius 0
	Height 0
	RenderStyle Add
	Alpha 0.01
	Scale 0.37
	
	+MISSILE
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	-SOLID
	
	States {
		
		Spawn:
			SPFX AAAAA 1 Bright A_FadeIn(0.2)
			SPFX AAAAAAAAAAAAAAAA 1 Bright A_FadeOut(0.06)
			Stop
	}
}

Actor WhitePowerPad {
	
	//$NotAngled
	//$Title "White power pad"
	//$Category Decorate/SFX/Power pads

	//$Arg0 "Diameter"
	//$Arg0ToolTip "Diameter, in a square area type this value will define x and y values"
	//$Arg0Type 0

	//$Arg1 "Area type"
	//$Arg1Type 11
	//$Arg1Enum { 0 = "Square"; 1 = "Circle"; }

	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	+NOINTERACTION
	+NOCLIP
	-SOLID
	+CLIENTSIDEONLY
	
		States {
			
			Spawn:
			Active:
				TNT1 A 0
				TNT1 A 0 A_JumpIf(Args[1], "Circle")
				TNT1 A 1 A_SpawnItemEx ("BaseLine", random(-Args[0], Args[0]), random(-Args[0], Args[0]), random(0, 8), 0, 0, random(1,3), 0, 129, 0) 
				Loop
			Circle:
				TNT1 A 0
				TNT1 A 1 A_SpawnItemEx ("BaseLine", random(-Args[0], Args[0]), 0, random(0, 8), 0, 0, random(1,3), Random(0, 360), 129, 0) 
				Loop
			Inactive:
				TNT1 A 1
				Loop
		}	  
		
}

Actor BluePowerPad : WhitePowerPad {
	
	//$Title "Blue power pad"
	Translation "80:111=196:207"
}

Actor GreenPowerPad : WhitePowerPad {
	
	//$Title "Green power pad"
	Translation "80:111=112:127"
}

Actor RedPowerPad : WhitePowerPad {
	
	//$Title "Red power pad"
	Translation "80:111=176:191"
}

Actor OrangePowerPad: WhitePowerPad {
	
	//$Title "Orange power pad"
	Translation "80:111=214:223"
}
