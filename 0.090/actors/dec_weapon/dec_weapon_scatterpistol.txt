ACTOR ScatterPistol : Weapon 15062 {
	
	//$NotAngled
	//$Category Decorate/Weapons
	//$Title "Scatter pistol"
	//$Sprite SPPPA0
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got a scatter pistol!"
	Weapon.AmmoType "NewClip"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	Weapon.SlotNumber 2
	Weapon.SlotPriority 0.9
	Decal PlasmaScorch

	States {

		Spawn:
			SPPP A -1
			Loop
		Ready:
			SPRP A 1 A_WeaponReady
			Loop
		Deselect:
			SPRP A 1 A_Lower
			Loop
		Select:
			SPRP A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound("SPRPSHOT", CHAN_WEAPON, 0.03)
			SPRP E 1 Bright A_FireCustomMissile("ScatterPShot", 0, 1, 0, 0, 0, 0)
			SPRP F 1 Bright
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound("SPRPSHOT", CHAN_WEAPON, 0.03)
			SPRP G 2 A_FireCustomMissile("ScatterPShot", 0, 1, 0, 0, 0, 0)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound("SPRPSHOT", CHAN_WEAPON, 0.03)
			SPRP G 1 A_FireCustomMissile("ScatterPShot", 0, 1, 0, 0, 0, 0)
			SPRP D 3
			SPRP G 3
			SPRP CB 4
			SPRP A 5
			SPRP A 1 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 1 Bright A_Light1
			TNT1 A 1 Bright A_Light0
			Stop
	}
}

ACTOR ScatterPShot : FastProjectile {
	
	Speed 70
	Radius 4
	Height 4
	Damage (random(1,2))
	Scale 0.2
	RenderStyle Add
	MissileHeight 8
	MissileType "ScatterPShotTrail"
	SeeSound "NDLSHOT"

	PROJECTILE
	+BRIGHT

	States {
		
		Spawn:
			SPPJ A 1 NoDelay A_SpawnItemEx("ScatterShotGlow", 0, 0, 0, 0, 0, 0, 0, 32)
			Loop
		Death:
			TNT1 A 0 A_PlaySound("SPRPIMPT", CHAN_WEAPON, 0.25)
			TNT1 A 0 A_SetScale(0.15)
			TNT1 AAAA 0 A_CustomMissile("ScatterSpark",0,0,random(0,360),CMF_AIMDIRECTION,random(-2,16))
			TNT1 AAA 0 A_CustomMissile("ScatterSpark",0,0,random(0,360),CMF_AIMDIRECTION,random(-40,-3))
			SPRI ABCDE 1
			Stop
	}
}

ACTOR ScatterSpark {
	
	Speed 15
	Radius 4
	Height 4
	Scale 0.2
	Damage (random(3,4))
	RenderStyle Add

	PROJECTILE
	+BRIGHT

	States {
		
		Spawn:
			SPPJ A 1 A_FadeOut(0.03)
			Loop
		Death:
			TNT1 A 0 A_PlaySound("SPRPIMPT", 5, 0.3, ATTN_STATIC)
			TNT1 A 0 A_SetScale(0.12)
			SPRI ABCDE 1
			Stop
	}
}

// AESTHETIC EFFECTS

ACTOR ScatterShotGlow {
	
	RenderStyle Add
	Alpha 0.4
	Scale 0.2

	+NOINTERACTION
	+THRUACTORS

	States {
		
		Spawn:
			SPGL A 1 Bright A_FadeOut(0,1)
			Loop
	}
}

ACTOR ScatterPShotTrail {
	
	Radius 6
	Height 8
	Scale 0.03
	RenderStyle Add
	Alpha 0.4

	+NOINTERACTION
	+THRUACTORS

	States {
		
		Spawn:
			NDTL A 1 A_FadeOut (0.1)
			Loop
	}
}