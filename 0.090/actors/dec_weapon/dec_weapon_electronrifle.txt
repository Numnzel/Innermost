Actor ElectronRifle : Weapon 15068 {

  //$NotAngled
  //$Category Decorate/Weapons
  //$Title "Electron rifle"
  //$Sprite LAUNA0
	Weapon.SlotNumber 8
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 30
	Weapon.AmmoGive 30
	Weapon.AmmoType1 "ElectronCell"
	Weapon.AmmoType2 "ElectronCharge"
	Inventory.PickupMessage "You picked up an electron rifle."
	
	+WEAPON.NOAUTOAIM
	+WEAPON.ALT_AMMO_OPTIONAL
	
	States {

	Ready:
		MISG A 1 A_WeaponReady
		TNT1 A 0 A_GiveInventory("TimeCountToTakeInv", 1)
		TNT1 A 0 A_JumpIfInventory("TimeCountToTakeInv", 280, "UnloadElectronCharge")
		TNT1 A 0 A_Jump(12, 1)
		Loop
		TNT1 A 0 A_PlayWeaponSound("weapon/lightningready")
		Loop
	UnloadElectronCharge:
		TNT1 A 0 A_TakeInventory("ElectronCharge", 1)
		TNT1 A 0 A_TakeInventory("TimeCountToTakeInv", 280)
		Goto Ready
	Deselect:
		TNT1 A 0 A_TakeInventory("ElectronCharge", 137) //Cooler than taking it all.
		MISG A 1 A_Lower
		Loop
	Select:
		MISG A 1 A_Raise
		Loop
	Fire:
		MISG B 8 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapon/lightningfire")
		TNT1 A 0 A_JumpIfInventory("ElectronCharge", 30, "FireOrbital") //If you have ECharge fire special, if not fire normal.
	FireDone:
		TNT1 A 0 A_GiveInventory("ElectronCharge", 3)
		MISG B 10 A_FireCustomMissile("SeekerElectronVolt", 0, 1, 0)
		MISG B 0 A_ReFire
		Goto Ready
	FireOrbital:
		TNT1 A 0 A_JumpIfInventory("ElectronCell", 2, "FireOrbitalDone") //If you have ammo fire special, if not fire normal.
		Goto FireDone
	FireOrbitalDone:
		TNT1 A 0 A_TakeInventory("ElectronCharge", 30)
		TNT1 A 0 A_TakeInventory("ElectronCell", 2)
		MISG B 10 A_FireCustomMissile("OrbiterElectronVolt", 0, 0, 0)
		MISG B 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("ElectronCell", 1, "AltFireDone") //If you have ammo fire normal, if not go to ready.
		Goto Ready
	AltFireDone:
		TNT1 A 0 A_TakeInventory("ElectronCell", 1)
		TNT1 A 0 A_GiveInventory("ElectronCharge", 3)
		MISG B 8 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapon/lightningfire")
		MISG B 10 A_FireCustomMissile("SeekerElectronVolt", 0, 0, 0)
		MISG B 0 A_ReFire
		Goto Ready
	Flash:
		MISF A 3 Bright A_Light1
		MISF B 4 Bright
		MISF CD 4 Bright A_Light2
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

Actor SeekerElectronVolt {

	Radius 6
	Height 8
	Damage 6
	Scale 0.5
	Speed 16
	Renderstyle Add
	Alpha 0.8
	AttackSound "weapon/lightningzap"
	ActiveSound "weapon/lightningcontinuous"
	Obituary "Electrified by an 'electron-volt' projectile."
	
	Projectile
	+RANDOMIZE
	+SEEKERMISSILE
	+NOEXTREMEDEATH
	+BRIGHT
	
	States {
	
	Spawn:
		FX16 AAABBBCCC 1 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
	Orbit:
		FX16 A 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 A 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		FX16 A 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 A 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		FX16 A 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 A 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		FX16 B 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 B 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		FX16 B 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 B 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		FX16 B 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 B 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		FX16 C 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 C 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		FX16 C 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 C 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		FX16 C 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 C 1 A_SeekerMissile(90, 90, SMF_LOOK|SMF_PRECISE, 255)
		Loop
	Death:
		FX16 GHIJKL 1 A_SetScale(scalex+0.01, scaley+0.01)
		Stop
	}
}

Actor OrbiterElectronVolt : SeekerElectronVolt {

	Damage 0
	
	States {
	
	Death:
		TNT1 A 0 A_CustomMissile("OrbitingElectronVolt", 0, 0, 0)
		Stop
	}
}

Actor ElectronVoltFX {
	
	Scale 0.5
	Renderstyle Add
	Alpha 0.7
	
	+NOINTERACTION
	
	States {
	
	Spawn:
		FX16 AAABBBCCC 1 A_FadeOut(0.07, 1)
		Loop
	Death:
		Stop
	}
}

ACTOR OrbitingElectronVolt : SeekerElectronVolt {

	Damage 1
	Speed 0
	ReactionTime 70
	
	+RIPPER
	
	var int user_angle;
	
	States {
	
	Spawn:
	Orbit1:
		FX16 A 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 A 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 A 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		TNT1 A 0 A_Countdown
		FX16 A 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit2")
		Goto Death
	Orbit2:
		FX16 A 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 A 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 A 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		FX16 A 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit3")
		Goto Death
	Orbit3:
		FX16 A 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 A 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 A 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		FX16 A 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit4")
		Goto Death
	Orbit4:
		FX16 B 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 B 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 B 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		TNT1 A 0 A_Countdown
		FX16 B 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit5")
		Goto Death
	Orbit5:
		FX16 B 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 B 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 B 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		FX16 B 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit6")
		Goto Death
	Orbit6:
		FX16 B 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 B 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 B 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		FX16 B 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit7")
		Goto Death
	Orbit7:
		FX16 C 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 C 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 C 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		TNT1 A 0 A_Countdown
		FX16 C 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit8")
		Goto Death
	Orbit8:
		FX16 C 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 C 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 C 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		FX16 C 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit9")
		Goto Death
	Orbit9:
		FX16 C 0 A_SetUserVar("user_angle", user_angle + 9)
		FX16 C 0 A_SpawnItemEx("ElectronVoltFX", 0, 0, 0, 0, 0, 0, 0)
		FX16 C 1 A_SeekerMissile(20, 20, SMF_LOOK|SMF_PRECISE, 255)
		FX16 C 0 A_Warp(AAPTR_TRACER, ReactionTime, 0, 32, user_angle, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, "Orbit1")
		Goto Death
	Death:
		TNT1 A 0 A_Explode(18, 160)
		FX16 GHIJKL 1 A_SetScale(scalex+0.2, scaley+0.2)
		Stop
	}
}

Actor ElectronCharge : Ammo {

	Inventory.Amount 3
	Inventory.MaxAmount 8000
	Inventory.Icon "CELPA0"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 8000
	
	States {

	Spawn:
		TNT1 A -1
		Stop
	}
}

Actor TimeCountToTakeInv : Inventory {

	Inventory.Amount 1
	Inventory.MaxAmount 280
}