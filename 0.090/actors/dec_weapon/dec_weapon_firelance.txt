// Firelance

Actor FireLance : Weapon 15008
{
  //$NotAngled
  //$Category Decorate/Weapons
  //$Title "Fire lance"
  //$Sprite KSWPA0
  Inventory.PickupSound "Misc/W_PkUp"
  Inventory.PickupMessage "You got a proton launcher 'FireLance' prototype!"
  Obituary "%o was fried by %k's FireLance!"
  Weapon.AmmoType "ProtonEnergyCell"
  Weapon.AmmoGive 2
  Weapon.AmmoUse 1
  Weapon.KickBack 1000
  Weapon.SlotNumber 8
  States
  {
  Spawn:
    KSWP A -1
    loop
  Select:
    KSAW C 1 A_Raise
    loop
  Deselect:
    KSAW C 1 A_Lower
    loop
  Ready:
    KSAW A 0 A_PlaySound("Weapons/KarasawaIdle")
    KSAW ABCB 4 A_WeaponReady
    KSAW AAAAAAA 5 A_WeaponReady
    loop
  Fire:
    KSAW A 0 A_PlayWeaponSound("Weapons/KarasawaFire")
    KSAW D 1 BRIGHT A_FireCustomMissile("ProtonicBall", 0, 1, 0, 0)
    KSAW E 1
    KSAW FGH 2 BRIGHT A_Light2
    KSAW I 0 A_PlaySoundEx("Weapons/KarasawaCharge", "SoundSlot7", 0)
    KSAW IA 4 A_Light1
    KSAW JKLMN 4
    KSAW A 0 A_Light0
    KSAW N 6
    Goto Ready
  }
}

Actor ProtonicBall
{
  Radius 10
  Height 6
  Speed 25
  Scale 0.8
  Damage 80
  Translation "112:127=214:223"
  RenderStyle Add
  Alpha 0.75
  DeathSound "weapons/bfgx"
  Obituary "$OB_MPBFG_BOOM"
  
  PROJECTILE
  +RANDOMIZE
  
  States
  {
  Spawn:
    BFS1 AB 4 BRIGHT A_SpawnItemEx("ProtonicBallTrail", 0, 0, 0, 0, 0, 0, 0, 0)
    Loop
  Death:
    BFE1 A 8 BRIGHT A_Explode(80,100,1,1)
    BFE1 B 8 BRIGHT
    BFE1 C 0 BRIGHT A_SpawnItemEx("PulseGenerator", 0, 0, 0, 0, 0, 0, 0, 129, 0)
    BFE1 C 0 BRIGHT A_SpawnItemEx("VerticalFireC", 0, 0, 0, 0, 0, 184, 0, 129, 0)
    BFE1 C 8 BRIGHT A_SpawnItemEx("VerticalFireF", 0, 0, 0, 0, 0, -184, 0, 129, 0)
    BFE1 D 8 BRIGHT A_SpawnItemEx("FireGenerator", 0, 0, 0, 0, 0, 0, 0, 129, 0)
    BFE1 EF 8 BRIGHT
    Stop
  }
}

Actor ProtonicBallTrail
{
  Translation "112:127=214:223"
  RenderStyle Add
  Alpha 0.75
  
  +NOBLOCKMAP
  +NOGRAVITY
  +RANDOMIZE
  
  States
  {
  Spawn:
    BFS1 AAAABBBB 1 BRIGHT A_FadeOut
    Loop
  }
}

Actor VerticalFireC : MageWandMissile
{
  Radius 4
  Height 3
  Speed 184
  Damage 10
  RenderStyle Add
  Alpha 0.8
  Scale 0.7
  SeeSound ""
  DeathSound "Weapons/KarasawaHit"
  MissileType "VerticalFireBlur"
  MissileHeight 8
  
  var int user_shockwave;
  
  PROJECTILE
  +RIPPER
  +NODAMAGETHRUST
  +CANNOTPUSH
  
  States
  {
  Spawn:
    TNT1 A 0 A_SetUserVar("user_shockwave", 0)
    //KWBB A 0 NoDelay BRIGHT A_PlaySound("KsawBoltFly")
    KWBB ABCD 1 BRIGHT //A_SpawnItemEx("VerticalFireBlur", 0, 0, 0, 0, 0, 0, 0, 129, 0)
    Loop
  Death:
    TNT1 A 0 A_CustomMissile("ProtonicWaveC",0,0,user_shockwave)
    TNT1 A 0 A_SetUserVar("user_shockwave",user_shockwave+10)
    TNT1 A 0 A_JumpIf(user_shockwave==360, 1)
    Loop
    KWBD ABCDEF 2 BRIGHT
    Stop
  }
}

Actor VerticalFireF : VerticalFireC
{
  States
  {
  Death:
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,0)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,10)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,20)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,30)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,40)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,50)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,60)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,70)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,80)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,90)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,100)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,110)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,120)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,130)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,140)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,150)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,160)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,170)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,180)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,190)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,200)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,210)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,220)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,230)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,240)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,250)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,260)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,270)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,280)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,290)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,300)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,310)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,320)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,330)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,340)
    KWBD A 0 A_CustomMissile("ProtonicWaveF",0,0,350)
    KWBD ABCDEF 2 BRIGHT
    Stop
  }
}

Actor VerticalFireBlur
{
  +NOBLOCKMAP
  +NOGRAVITY
  +RANDOMIZE
  States
  {
  Spawn:
    KWBB ABCD 1 BRIGHT A_FadeOut(0.005)
    Loop
  }
}

Actor ProtonicWaveC
{
  Height 2
  Radius 5
  Damage 1
  Speed 5
  RenderStyle Add
  Alpha 0.6
  Scale 0.5
  
  PROJECTILE
  +RIPPER
  +NODAMAGETHRUST
  +CEILINGHUGGER
  states
  {
  Spawn:
  Death:
    KWBR AB 4 BRIGHT A_FadeOut(0.075)
    Loop
  }
}

Actor ProtonicWaveF : ProtonicWaveC
{
  +FLOORHUGGER
  -CEILINGHUGGER
}

ACTOR PulseGenerator
{
   +NOBLOCKMAP
   +NOGRAVITY
   +NOSECTOR
   +NOINTERACTION
   +NOCLIP
   +CLIENTSIDEONLY
   -SOLID
   States
   {
   Spawn:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx ("BaseLine2", random(-24, 24), 0, random(0, 8), 0, 0, random(random(-2,-7),random(2,7)), Random(1, 360), 129, 0)
      Stop
   }
}

ACTOR FireGenerator : PulseGenerator
{
   States
   {
   Spawn:
      TNT1 AAAAAAAAAA 0 A_CustomMissile ("NukeFire", 0, 0, random(1, 360), 2, random(45, 135))
      Stop
   }
}

ACTOR BaseLine2
{
   +MISSILE
   +NOGRAVITY
   +NOBLOCKMAP
   +NOINTERACTION
   -SOLID
   Radius 0
   Height 0
   RenderStyle Add
   Alpha 0.01
   Scale 1
   States
   {
   Spawn:
      TNT1 A 0 A_Jump(32,"Size3")
      TNT1 A 0 A_Jump(64,"Size2")
      TNT1 A 0 A_Jump(96,"Size05")
   Spawn1:
      SPFX BBBBB 4 BRIGHT A_FadeIn(0.2) 
      SPFX BBBBBBBBBBBBBBBB 4 Bright A_FadeOut(0.06)
      Stop
   Size2:
      TNT1 A 0 A_SetScale(2,2)
      Goto Spawn1
   Size3:
      TNT1 A 0 A_SetScale(3,3)
      Goto Spawn1
   Size05:
      TNT1 A 0 A_SetScale(0.5,0.5)
      Goto Spawn1
   }
}

ACTOR NukeFire
{
   Radius 8
   Height 20
   Speed 5
   Mass 100
   -SOLID
   +DROPOFF
   Renderstyle Add
   Alpha 0.2
   YScale 0.3
   XScale 0.2
   DeathSound "weapons/nuclearmissileburn"
   States
   {
   Spawn:
      NKFR A 0 A_Jump(128,14)
      NKFR ABCDEFGHIJKLMN 4 BRIGHT
      NKFR ABCDEFGHIJKLMN 4 BRIGHT
   Death:
      NKFR O 5 BRIGHT
      NKFR P 7 BRIGHT
      NKFR Q 10 BRIGHT
      Stop
   }
}

Actor ProtonEnergyCell : Ammo 15015
{
//$NotAngled
//$Category Decorate/Ammo
//$Title "Proton cell"
  Radius 8
  Height 8
  Inventory.PickupMessage "Picked up a proton cell."
  Inventory.Icon "KWASC0"
  Inventory.Amount 2
  Inventory.MaxAmount 20
  Ammo.BackpackAmount 2
  Ammo.BackpackMaxAmount 40
  States
  {
  Spawn:
    KWAS ABCD 4
    Loop
  }
}

ACTOR ProtonEnergyCellPack : ProtonEnergyCell 15014
{
//$Title "Proton cell pack"
  Radius 16
  Height 8
  Inventory.PickupMessage "Picked up a proton cell pack."
  Inventory.Icon "KWASC0"
  Inventory.Amount 6
  States
  {
  Spawn:
    KWAL ABCD 4
    Loop
  }
}