ACTOR XimRocketGuy {

	//$Category Decorate/Monsters
	//$Title "Former rocket launcher"
	
	Health 80
	Obituary "%o was 'sploded by a rocketlauncher."
	Radius 20
	Height 56
	Mass 100
	Speed 8
	Painchance 170

	SeeSound "shotguy/sight"
	AttackSound "shotguy/attack"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	ActiveSound "shotguy/active"

	Dropitem "NewRocketAmmo"
	DropItem "BitCoinWhiteDataCard"
	DropItem "BitCoinWhiteDataCard"
	DropItem "BitCoinWhiteDataCard"

	MONSTER
	+FLOORCLIP

	States {
		
		Spawn:
			MPO2 AB 10 A_Look
			Loop
		See:
			MPO2 AABBCCDD 3 A_Chase
			Loop
		Missile:
			MPO2 E 10 A_FaceTarget
			MPO2 F 10 bright A_CyberAttack
			MPO2 E 10
			Goto See
		Pain:
			MPO2 G 2
			MPO2 G 2 A_Pain
			Goto See
		Death:
			MPO2 H 5
			MPO2 I 5 A_Scream
			MPO2 J 5
			MPO2 K 5 A_Fall
			MPO2 L -1
			Stop
		XDeath:
			MPO2 M 5
			MPO2 N 5 A_XScream
			MPO2 O 5
			MPO2 P 5 A_Fall
			MPO2 QRST 5
			MPO2 U -1
			Stop
		Raise:
			MPO2 MJKJI 5
			Goto See
	}
}