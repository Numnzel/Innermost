ACTOR Wishmaster 15094 {

	//$Category Decorate/Monsters
	//$Title "Wishmaster"
	
	// Hacer una variante de archvile con telefrag (instakill) ??
	
	Health 700
	Radius 20
	Height 56
	Mass 500
	Speed 15
	PainChance 10
	MaxTargetRange 224 //896 //32
	
	DamageFactor Star, 0.0
	
	MONSTER
	+QUICKTORETALIATE
	+FLOORCLIP
	+NOTARGET
	
	SeeSound "vile/sight"
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/stop"
	
	Obituary "%o died by the desires of the Wishmaster."
	
	States {
	
		Spawn:
			WISH AB 10 A_Look
			Loop
		See:
			WISH G 0 Bright A_ChangeFlag(NOTARGETSWITCH, FALSE)
			WISH AABBCCDDEEFF 2 A_Chase
			// WISH A 1 // A_RadiusGive ("Regenerator", 1024.0, RGF_CORPSES|RGF_KILLED, 1) // A_CheckProximity("Consume", "", 1024.0, 1, CPXF_DEADONLY)
			Loop
		Missile:
			WISH G 0 Bright A_ChangeFlag(NOTARGETSWITCH, TRUE)
			WISH G 0 Bright A_VileStart
			WISH G 10 Bright A_FaceTarget
			WISH G 0 Bright A_SpawnItemEx("WishmasterFire", 0, 0, 8, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_ISTRACER)
			WISH GGGGGGG 15 Bright A_FaceTarget // 15 * 7 = 105 = 3 s channeling
			WISH HIJKLMN 8 Bright A_FaceTarget // 7 * 8 = 56
			// WISH O 0 Bright A_Jump(32, "DoWish")
			WISH O 8 Bright A_KillTracer ("Desire", 0, "WishmasterFire")
			WISH P 20 Bright // A_RemoveTracer(RMVF_MISC, "WishmasterFire")
			Goto See
		/*DoWish:
			WISH O 8 Bright A_GiveToTarget("Health", random(80,100))
			WISH P 20 Bright
			Goto See*/
		Consume:
			WISH "[\]" 10 Bright
			Goto See
		Pain:
			WISH Q 5 A_RemoveTracer (RMVF_EVERYTHING, "WishmasterFire")
			WISH Q 5 A_Pain
			Goto See
		Death:
			WISH Q 7 A_RemoveTracer (RMVF_EVERYTHING, "WishmasterFire")
			WISH R 7 A_Scream
			WISH S 7 A_NoBlocking
			WISH TUVWXY 7
			WISH Z -1
			Stop
	}
}


ACTOR WishmasterFire {

	+NOBLOCKMAP
	+NOGRAVITY
	+SHOOTABLE
	
	Radius 10
	Height 32
	
	ReactionTime 175 // 5 seconds, security time.
	
	States {
		
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("vile/firecrkl")
		Channeling:
			TNT1 A 1 A_Warp (AAPTR_TARGET, 24, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
			TNT1 A 0 A_SpawnItemEx ("WishmasterFireSpark", 0, 0, 8, 0, 0, random(1,2), 0, SXF_SETMASTER|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_Countdown
			Loop
		Death.Desire:
			TNT1 A 0 A_PlaySound("vile/stop")
			TNT1 A 0 A_Explode(70, 70, -XF_HURTSOURCE, 0, 0, 0, 0, "BulletPuff", "Star")
		Death:
			Stop
	}
}


ACTOR WishmasterFireSpark {

	+NOBLOCKMAP
	+NOGRAVITY
	+RANDOMIZE
	
	RenderStyle Add
	Alpha 0.0
	// Translation Ice
	Scale 0.025
	Gravity 1
	
	var int user_pos;
	var int user_angle;
	
	States {
		
		Spawn:
			TNT1 A 0 NoDelay A_SetScale (frandom(0.005, 0.025))
			TNT1 A 0 A_SetUserVar ("user_pos", random(-80,80))
			TNT1 A 0 A_SetUserVar ("user_angle", random(1, 360))
			STAR ABCDEFGHIJKLMNOPQR 2 Bright { A_Warp(AAPTR_MASTER, user_pos, 0, z, user_angle, SXF_NOCHECKPOSITION|WARPF_TOFLOOR|WARPF_INTERPOLATE); A_FadeIn(0.035); }
		Repeat:
			STAR ABCDEFGHIJKLMNOPQR 2 Bright { A_Warp(AAPTR_MASTER, user_pos, 0, z, user_angle, SXF_NOCHECKPOSITION|WARPF_TOFLOOR|WARPF_INTERPOLATE); A_FadeOut(0.035); }
			Loop
	}
}





Actor Regenerator : CustomInventory {
	
	Inventory.Amount 1
	Inventory.MaxAmount 0
	+INVENTORY.AUTOACTIVATE
	
	States {
		
		Spawn:
			TNT1 A 3
			Loop
		Use:
			TNT1 A 0 A_JumpIfInventory("Regenerated", 0, "Remains")
			TNT1 A 0 A_GiveInventory("Regenerated")
			TNT1 A 0 A_Respawn
			Stop
		Remains:
			TNT1 A 0
			Fail
	}
}

Actor Regenerated : Inventory {
	
	Inventory.Amount 1
	Inventory.MaxAmount 1
}