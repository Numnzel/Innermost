ACTOR Guardian {

	//$Category Decorate/Monsters
	//$Title "Guardian cube"
	
	Health 80
	Radius 16
	Height 32
	Mass 150
	Speed 8
	PainChance 64
	HitObituary "%o got fried by a Guardian Cube."
	Obituary "%o got fried by a Guardian Cube."
	
	MONSTER
	+NOGRAVITY
	+FLOAT
	+DONTFALL
	+NOBLOOD
	+DONTHURTSPECIES
	
	SeeSound "monster/cubsit"
	PainSound "weapons/bounc1"
	ActiveSound "monster/cubfly"
	DeathSound "monster/cubdth"
	
	SpawnID 172

	States {
		
		Spawn:
			CUBE A 10 A_Look
			Loop
		See:
			CUBE ABC 5 A_Chase
			Loop
		Missile:
			CUBE D 5 BRIGHT A_FaceTarget
			CUBE E 0 BRIGHT A_CustomMissile("CubeShot",10,4,0,1)
			CUBE E 4 BRIGHT A_CustomMissile("CubeShot",10,-4,0,1)
			CUBE D 4 BRIGHT A_FaceTarget
			CUBE E 0 BRIGHT A_CustomMissile("CubeShot",10,4,0,1)
			CUBE E 4 BRIGHT A_CustomMissile("CubeShot",10,-4,0,1)
			CUBE D 4 BRIGHT A_FaceTarget
			CUBE E 0 BRIGHT A_CustomMissile("CubeShot",10,4,0,1)
			CUBE E 4 BRIGHT A_CustomMissile("CubeShot",10,-4,0,1)
			CUBE D 4 BRIGHT A_FaceTarget
			Goto See
		Pain:
			CUBE D 3
			CUBE D 3 A_Pain
			Goto See
		Death:
			CUBE E 0 BRIGHT A_NoBlocking
			CUBE E 0 BRIGHT A_SetFloorClip
			CUBE E 6 BRIGHT A_Scream
			CUBE FGH 6 BRIGHT
			Stop
	}
}

ACTOR CubeShot {
	
	Radius 5
	Height 5
	Speed 15
	Damage 1
	DamageType Fire
	Renderstyle Add
	Alpha 0.85
	
	PROJECTILE
	+THRUGHOST
	
	Seesound "weapons/firsfi"
	DeathSound "weapons/maceex"
	
	Obituary "%o got fried by a Guardian Cube."
	
	States {
		
		Spawn:
		   CBAL AB 3 Bright A_SpawnItem("CubePuff",0,0)
		   Loop
		Death:
		   CBAL CDEFG 3 Bright
		   Stop
	}
}

ACTOR CubePuff {
	
	Radius 2
	Height 2
	Speed 0
	Scale 1.0
	
	Renderstyle Add
	Alpha 0.67
	
	PROJECTILE
	
	States {
		
		Spawn:
		   CBAL HIJ 3 BRIGHT
		   Stop
	}
}