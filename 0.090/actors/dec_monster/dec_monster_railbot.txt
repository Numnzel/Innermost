ACTOR RailBot 15034
{
//$Category Decorate/Monsters
//$Title "Rail bot"
  Health 120
  Radius 16
  Height 16
  Speed 0
  Painchance 128
  Mass 0xFFFFFFFFFF
  obituary "%o was zapped by a railbot."
  
  painsound "Railbot/pain"
  deathsound "Railbot/death"
  
  Monster
  +QUICKTORETALIATE
  +SPAWNCEILING
  +NOGRAVITY
  +NOBLOOD
  +NOSPLASHALERT
  +DONTFALL
  +NOTELEPORT
  +DONTHARMCLASS
  +DONTDRAIN
  +NOTRIGGER
  +LOOKALLAROUND
  +DONTTHRUST
  
  States
  {
  Spawn:
    AGUN A 5 A_Look
    Loop
  See:
    AGUN AAAAAAAAAA 2 A_Chase
    AGUN A 0 A_PlaySound ("Railbot/Beep")
    Loop
  Missile:
    AGUN A 6 A_FaceTarget
    AGUN C 2
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, -4, 0, 1)
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, 4, 0, 1)
    AGUN B 2 
    AGUN A 2 A_FaceTarget
    AGUN C 2
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, -4, 0, 1)
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, 4, 0, 1)
    AGUN B 2
    AGUN A 2 A_FaceTarget
    AGUN C 2
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, -4, 0, 1)
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, 4, 0, 1)
    AGUN B 2
    AGUN A 2 A_FaceTarget
    goto See
  Pain:
    AGUN A 2 A_Pain
    AGUN A 0 A_Chase
    goto See
  Death:
    AGUN A 48 A_Scream
    AGUN A 4 
    AGUN A 0 A_SpawnItemEx (RailBotBoom, 0, 0, -4, 0, 0, 0, 0, 128)
    AGUN A 0 A_PlaySound ("weapons/rocklx")
    AGUN D -1
    Stop
  }
}

Actor RailBotBeam : MageWandMissile
{
  speed 30
  damage 3
  -RIPPER
  -BLOODSPLATTER
  +SPAWNSOUNDSOURCE
  +NOEXTREMEDEATH
  +STRIFEDAMAGE
  SeeSound "Railbot/Fire"
  DeathSound "Laser/Bounce"
  Decal Bulletchip
  States
  {
  Death:
    TNT1 A 1
    //TNT1 AAAAAAAAAAAAAAA 0 bright A_SpawnItemEx (BeamParticle, 0, 0, 0, random(1,4), random(1,4), random(1,-1), random(0,360), 128)
    Stop
  }
}

ACTOR BeamTrail : MageWandSmoke replaces MageWandSmoke
{
  RenderStyle Add
  Alpha 0.75
  Scale 0.025
  +NOINTERACTION
  States
  {
  Spawn:
    OPRT A 2 bright
  Fade:
    OPRT A 1 bright A_FadeOut(0.1)
    loop
  }
}

Actor BeamParticle
{
  radius 2
  height 2
  speed 0
  damage 0
  scale 0.005
  RenderStyle Add
  Alpha 0.75
  +MISSILE
  +LOWGRAVITY
  +NOBLOCKMAP
  +NOTELEPORT
  +NOCLIP
  +DONTSPLASH
  -BLOODSPLATTER
  states
  {
  Spawn:
    OPRT B 2 bright A_FadeOut(0.1)
    loop
  }
}

actor RailBotBoom
{
  Radius 2
  Height 2
  Speed 0
  Damage 0
  Scale 0.75
  renderstyle Add
  Projectile
  +NOCLIP
  States
  {
  Spawn:
  Death:
    MISL B 8 bright
    MISL C 6 bright
    MISL D 4 bright
    stop
  }
}
